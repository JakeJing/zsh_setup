# zsh key bindings
# # Extended word movements/actions
autoload -Uz select-word-style
function _zle-with-style() {
	setopt localoptions
	unsetopt warn_create_global
	local style
	local WORDCHARS='*?_[]~=&;!#$%^(){}<>'
	[[ -n "$3" ]] && WORDCHARS=${WORDCHARS/\/}
	[[ $BUFFER =~ '^\s+$' ]] && style=shell || style=$2
	select-word-style $style
	zle $1
	[[ -n "$3" ]] && WORDCHARS=${WORDCHARS/\/}
	select-word-style normal
}

function _forward-word()		{ _zle-with-style forward-word			bash }
function _backward-kill-word()	{ _zle-with-style backward-kill-word 	normal }

zle -N _forward-word
zle -N _backward-kill-word
bindkey '\C-[OC'	_forward-word		# ctrl-right
bindkey '\C-w'		_backward-kill-word

# Allow more powerful history-i-search (multiple uses in the same line)
autoload -Uz narrow-to-region
function _history-incremental-preserving-pattern-search-backward() {
	local state tmp
	MARK=CURSOR  # magick, else multiple ^R don't work
	narrow-to-region -p "$LBUFFER${BUFFER:+>>}" -P "${BUFFER:+<<}$RBUFFER" -S state
	zle end-of-history
	zle history-incremental-pattern-search-backward
	narrow-to-region -R state
}
zle -N _history-incremental-preserving-pattern-search-backward
bindkey '^r' _history-incremental-preserving-pattern-search-backward
bindkey -M isearch '^r' history-incremental-pattern-search-backward
bindkey '^s' history-incremental-pattern-search-forward
